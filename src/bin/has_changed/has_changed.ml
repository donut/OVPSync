
let video (module Log : Logger.Sig) ~check_md5 old new' =
  let open Lwt.Infix in
  let open Rdb_dest.Video in

  let module C = Check.Make (struct
    type result = bool Lwt.t

    let runner ~has_changed ~to_log a b : bool Lwt.t =  
      if has_changed a b then
        let log_id = old.canonical.media_id in
        let%lwt () = Log.infof "[%s] %s." log_id (to_log a b) in
        Lwt.return true

      else Lwt.return false
  end) in

  let or' ?(skip=false) name check old new' result =
    if result || skip then Lwt.return result
    else check name ~old ~new'
  in

  (* At first I tried just comparing update/modified timestamps and the MD5
     of the video file, but because the source may do clean up on the item
     after giving it to the destination, these timestamps are likely to change. 
     *)
  C.string_field "title" ~old:old.title ~new':new'.title
  >>= or' "slug" C.string_field old.slug new'.slug 

  >>= or' "publish" C.int_field old.publish new'.publish
  >>= or' "expires" C.(optional int_field) old.expires new'.expires

  >>= or' "thumbnail_uri" C.saveable_uri_field 
            old.thumbnail_uri new'.thumbnail_uri
  >>= or' "file_uri" C.saveable_uri_field 
            old.file_uri new'.file_uri

  (* filename is not checked, as the name stored in the database is based on
     the name of the file saved. This may have extra data attached and may not
     be taken exactly from what is passed from the source. *)

  (* We may not wan to check MD5 if it's not based on the file. For example,
     with JW, videos can just be URLs to a video file. In that case, the MD5
     is generated from the URL of the file, not the file itself and will
     always be different from the MD5 generated by this destination. *)
  >>= or' ~skip:(not check_md5) "md5" C.(optional string_field) old.md5 new'.md5

  (* width & height are not checked. Some OVPs don't make the original video
     file available by default, requiring changes to be applied to the asset
     before it can be accessed (JW is like this). This includes information
     about its width and height. We don't want to require that when checking
     if it has changed. The MD5 would change if the width and height had
     changed anyway. *)

  >>= or' "duration" C.(optional int_field) old.duration new'.duration
  >>= or' "description" C.(optional string_field) 
            old.description new'.description
  >>= or' "tags" C.string_list_field old.tags new'.tags
  >>= or' "cms_id" C.(optional string_field) old.cms_id new'.cms_id
  >>= or' "link" C.(optional uri_field) old.link new'.link
  >>= or' "custom" C.string_pair_list_field old.custom new'.custom
  >>= or' "canonical" C.source_field old.canonical new'.canonical
  >>= or' "sources" C.source_list_field old.sources new'.sources